package laya.webgl.resource {
	import laya.maths.Matrix;
	import laya.renders.Render;
	import laya.resource.Context;
	import laya.resource.Texture;
	import laya.utils.Browser;
	import laya.utils.ColorUtils;
	import laya.utils.FontInfo;
	import laya.utils.HTMLChar;
	import laya.utils.Stat;
	import laya.utils.WordText;
	import laya.webgl.canvas.WebGLContext2D;
	/**
	 * key:font
	 *    ä¸‹é¢æ˜¯å„ç§å¤§å°çš„page
	 * 	   æ¯ä¸ªå¤§å°çš„pageå¯ä»¥æœ‰å¤šä¸ª
	 */
	public class CharBook {
		//config
		//public static var minTextureWidth:Boolean = true;	//textureä¸ç”¨potã€‚å¯ä»¥å‡å°‘ä¸€ç‚¹å¤§å° è¿™ä¸ªçœä¸äº†å¤šå°‘ï¼Œè€Œä¸”ä¸åˆ©äºé‡ç”¨ã€‚
		public static var textureWidth:int = 512;			//ç¼ºçœå¤§å°
		public static var cacheRenderInfoInWordText:Boolean = true;	//åœ¨wordtextä¸­ç¼“å­˜è§£æç»“æœ
		public static var scaleFontWithCtx:Boolean = true;		//å¦‚æœæœ‰ç¼©æ”¾ï¼Œåˆ™ä¿®æ”¹å­—ä½“ï¼Œä»¥ä¿è¯æ¸…æ™°åº¦
		public static var gridSize:int = 16;		//æ ¼å­å•ä½
		public static var debug:Boolean = false; 			//æ˜¾ç¤ºè°ƒè¯•ç”¨è¾¹æ¡†
		
		//private var fontPages:*= { };	//ä»¥font familyä¸ºkeyã€‚é‡Œé¢æ˜¯ä¸€ä¸ªä»¥heightä¸ºkeyçš„pageåˆ—è¡¨
		private var fontPages:Array = [];//ç”¨æ•°ç»„æ–¹å¼ä¿å­˜ CharPages, åŸæ¥ç”¨Objectä¸åˆ©äºéå†
		private var fontPagesName:Array = [];//ä¸Šé¢æ•°ç»„å¯¹åº”çš„å­—ä½“åç§°ã€‚
		static public var _curFont:String;	//å½“å‰é€‰ä¸­çš„font
		private var _curPage:CharPages;	//å½“å‰é€‰ä¸­çš„page
		
		private var tempUV:Array = [0, 0, 1, 0, 1, 1, 0, 1];	//è·å¾—æ–‡å­—çš„çº¹ç†åæ ‡ç”¨ï¼Œé¿å…æ¯æ¬¡éƒ½åˆ›å»ºå¯¹è±¡
		private var tempMat:Matrix = new Matrix();				//ç”¨æ¥å»æ‰contextçš„ç¼©æ”¾çš„çŸ©é˜µ
		private var fontScaleX:Number = 1.0;						//ä¸´æ—¶ç¼©æ”¾ã€‚
		private var fontScaleY:Number = 1.0;
		private var _curStrPos:int = 0;		//è§£å¼€ä¸€ä¸ªå­—ç¬¦ä¸²çš„æ—¶å€™ç”¨çš„ã€‚è¡¨ç¤ºå½“å‰è§£åˆ°ä»€ä¹ˆä½ç½®äº†
		public static var charbookInst:CharBook = null;	//debug
		public static var _fontMem:int = 0;		//æ–‡å­—æœ¬èº«å ç”¨çš„å†…å­˜ã€‚ä¸è€ƒè™‘mapå¯¹è±¡
		
		static private var _lastFont:String;
		static private var _lastFontSz:int = 0;
		static private var _lastCharPage:CharPages = null;
		static private var _uint32:Uint32Array = new Uint32Array(1);
		public static var trash:charPageTrash = new charPageTrash(textureWidth);
		public static var isWan1Wan:Boolean = false;
		
		public function CharBook() {
			var bugIOS:Boolean = false;//æ˜¯å¦æ˜¯æœ‰bugçš„iosç‰ˆæœ¬
			//åœ¨å¾®ä¿¡ä¸‹æœ‰æ—¶å€™ä¸æ˜¾ç¤ºæ–‡å­—ï¼Œæ‰€ä»¥é‡‡ç”¨canvasæ¨¡å¼ï¼Œç°åœ¨æµ‹è¯•å¾®ä¿¡å¥½åƒéƒ½å¥½äº†ï¼Œæ‰€ä»¥å»æ‰äº†ã€‚
			var miniadp:* = Laya['MiniAdpter'];
			if ( miniadp && miniadp.systemInfo && miniadp.systemInfo.system) {
				bugIOS = miniadp.systemInfo.system.toLowerCase() === 'ios 10.1.1';
			}
			if (Browser.onMiniGame /*&& !Browser.onAndroid*/ && !bugIOS ) isWan1Wan = true; //android å¾®ä¿¡ä¸‹ å­—è¾¹ç¼˜å‘é»‘ï¼Œæ‰€ä»¥ä¸ç”¨getImageDataäº†
			charbookInst = this;
			Laya['textRender'] = this;
			CharPages.charRender = Render.isConchApp ? (new CharRender_Native()) : (new CharRender_Canvas(textureWidth, textureWidth,scaleFontWithCtx,!isWan1Wan,debug));
		}
		
		/**
		 * é€‰æ‹©ä¸€ä¸ªåˆé€‚å¤§å°çš„pageã€‚ è¿™é‡Œä¼šè€ƒè™‘æ•´ä½“ç¼©æ”¾ã€‚
		 * @param	fontFamily
		 * @param	fontsize		è¿™ä¸ªæ˜¯åŸå§‹å¤§å°ï¼Œæ²¡æœ‰ç¼©æ”¾çš„
		 * @return
		 */
		public function selectFont(fontFamily:String, fontsize:int):CharPages {
			var scale:Number = Math.max(fontScaleX, fontScaleY);	//å–xyç¼©æ”¾ä¸­çš„å¤§çš„
			var scaledFontSz:int = fontsize * scale;//å–xyç¼©æ”¾ä¸­çš„å¤§çš„
			var ret:CharPages;
			if (fontFamily === _lastFont && scaledFontSz === _lastFontSz) {
				ret = _lastCharPage;
			} else {
				var sz:int = CharPages.getBmpSize(fontsize) * scale;//fontsize;//
				//æ¯çº§16
				var szid:int = Math.floor(sz / gridSize);
				var key:String = fontFamily + szid;
				var fid:int = fontPagesName.indexOf(key);
				if (fid < 0) {
					var selFontPages:CharPages = new CharPages(fontFamily, sz, Render.isConchApp ? 0 : Math.ceil((fontsize / 4.0)));
					fontPages.push(selFontPages);
					fontPagesName.push(key);
					ret = selFontPages;
				} else {
					ret = fontPages[fid];
				}
				ret.selectSize(fontsize, sz);
				_lastFont = fontFamily;
				_lastFontSz = scaledFontSz;
				_lastCharPage = ret;
			}
			return ret;
		}
		
		/**
		 * ä»stringä¸­å–å‡ºä¸€ä¸ªå®Œæ•´çš„charï¼Œä¾‹å¦‚emojiçš„è¯è¦å¤šä¸ª
		 * ä¼šä¿®æ”¹ _curStrPos
		 * TODO ç”±äºå„ç§æ–‡å­—ä¸­çš„ç»„åˆå†™æ³•ï¼Œè¿™ä¸ªéœ€è¦èƒ½æ‰©å±•ï¼Œä»¥ä¾¿æ”¯æŒæ³°æ–‡ç­‰
		 * @param	str
		 * @param	start	å¼€å§‹ä½ç½®
		 */
		public function getNextChar(str:String):String {
			var len:int = str.length;
			var start:int = _curStrPos;
			if (start >= len)
				return null;
			
			var link:Boolean = false;	//å¦‚æœæ˜¯è¿æ¥çš„è¯è¦å†åŠ ä¸€ä¸ªå®Œæ•´å­—ç¬¦
			var i:int = start;
			var state:int = 0; //0 åˆå§‹åŒ– 1  æ­£å¸¸ 2 è¿ç»­ä¸­
			for (; i < len; i++) {
				var c:int = str.charCodeAt(i);
				if ((c >>> 11) == 0x1b) { //å¯èƒ½æ˜¯0b110110xxæˆ–è€…0b110111xxã€‚ è¿™éƒ½è¡¨ç¤º2ä¸ªu16ç»„æˆä¸€ä¸ªemoji
					if (state == 1) break;//æ–°çš„å­—ç¬¦äº†ï¼Œè¦æˆªæ–­
					state = 1;	// å…¶ä»–çŠ¶æ€éƒ½è½¬æˆæ­£å¸¸è¯»å–å­—ç¬¦çŠ¶æ€ï¼Œåªæ˜¯ä¸€æ¬¡è¯»ä¸¤ä¸ª
					i++;	//è·¨è¿‡ä¸€ä¸ªã€‚
				}
				else if (c === 0xfe0e || c === 0xfe0f) {	//æ ·å¼æ§åˆ¶å­—ç¬¦
					// ç»§ç»­ã€‚ä¸æ”¹å˜çŠ¶æ€
				}
				else if (c == 0x200d) {		//zero width joiner
					state = 2; 	// ç»§ç»­
				}else {
					if (state == 0) state = 1;
					else if (state == 1) break;
					else if (state == 2) {
						// ç»§ç»­
					}
				}
			}
			_curStrPos = i;
			return str.substring(start, i);
		}
		
		/**
		 * æ£€æŸ¥ txtsæ•°ç»„ä¸­æœ‰æ²¡æœ‰è¢«é‡Šæ”¾çš„èµ„æº
		 * @param	txts {{ri:CharRenderInfo,...}[][]}
		 * @param	startid
		 * @return
		 */
		//TODO:coverage
		public function hasFreedText(txts:Array, startid:int):Boolean {
			if (txts && txts.length > 0) {
				for (var i:int = startid, sz:int = txts.length; i < sz; i++) {
					var pri:* = txts[i];
					if (!pri) continue;
					for (var j:int = 0, pisz:int = pri.length; j < pisz; j++) {
						var riSaved:CharRenderInfo = (pri[j] as Object).ri;
						if (riSaved.deleted || riSaved.tex.__destroyed) {
							return true;
						}
					}
				}
			}
			return false;
		}
		
		/**
		 * å‚æ•°éƒ½æ˜¯ç›´æ¥çš„ï¼Œä¸éœ€è¦è‡ªå·±å†ä»å­—ç¬¦ä¸²è§£æ
		 * @param	ctx
		 * @param	data
		 * @param	x
		 * @param	y
		 * @param	fontObj
		 * @param	color
		 * @param	strokeColor
		 * @param	lineWidth
		 * @param	textAlign
		 * @param	underLine
		 */
		public function _fast_filltext(ctx:WebGLContext2D, data:WordText, htmlchars:Vector.<HTMLChar>, x:Number, y:Number, font:FontInfo, color:String, strokeColor:String, lineWidth:int, textAlign:int, underLine:int = 0):void {
			if (data && data.length < 1) return;
			if (htmlchars && htmlchars.length < 1) return;
			
			//var st = PerfHUD.inst.now();
			_curFont = font._font;
			//
			fontScaleX = fontScaleY = 1.0;
			if (scaleFontWithCtx) {
				var sx:Number = ctx.getMatScaleX();// _curMat.getScaleX();
				var sy:Number = ctx.getMatScaleY();// _curMat.getScaleY();
				if (sx < 1e-4 || sy < 1e-1)
					return;
				if (sx > 1) fontScaleX = sx;
				if (sy > 1) fontScaleY = sy;
			}
			
			font._italic && (ctx._italicDeg = 12);
			_curPage = selectFont(font._family, font._size);
			//å‡†å¤‡bmp
			//æ‹·è´åˆ°textureä¸Š,å¾—åˆ°ä¸€ä¸ªgltextureå’Œuv
			var curx:Number = x;
			var wt:WordText = data as WordText;
			var str:String = data as String;
			var strWidth:Number = 0;
			var isWT:Boolean = !htmlchars && (data is WordText);
			var isHtmlChar:Boolean = !!htmlchars;
			/**
			 * sameTexData
			 * ä¸ºäº†åˆå¹¶ç›¸åŒè´´å›¾çš„ï¼Œè¦æ ¹æ®è´´å›¾æ¥åˆ†ç»„
			 * ä¸‹æ ‡æ˜¯textureid,å†…å®¹æ˜¯ä¸€ä¸ªvectorï¼Œä¿å­˜çš„å…·ä½“å†…å®¹è§ä¸‹é¢çš„ add.push({ri: ri...
			 */
			var sameTexData:Array = (cacheRenderInfoInWordText && isWT) ? wt.pageChars : [];
			
			//æ€»å®½åº¦ï¼Œä¸‹é¢çš„å¯¹é½éœ€è¦
			if (isWT) {
				str = wt._text;
				strWidth = wt.width;
				if (strWidth < 0) {
					strWidth = wt.width = _curPage.getWidth(str);
				}
			} else {
				strWidth = str?_curPage.getWidth(str):0;
			}
			
			//æ°´å¹³å¯¹é½æ–¹å¼
			switch (textAlign) {
			case Context.ENUM_TEXTALIGN_CENTER: 
				curx = x - strWidth / 2;
				break;
			case Context.ENUM_TEXTALIGN_RIGHT: 
				curx = x - strWidth;
				break;
			default: 
				curx = x;
			}
			
			//æ£€æŸ¥ä¿å­˜çš„æ•°æ®æ˜¯å¦æœ‰çš„å·²ç»è¢«é‡Šæ”¾äº†
			if (wt && wt.lastGCCnt != _curPage.gcCnt) {
				wt.lastGCCnt = _curPage.gcCnt;
				if (hasFreedText(sameTexData, wt.startID)) {// || hasFreedText(sameTexDataStroke,wt.startIDStroke))) {
					sameTexData = wt.pageChars = [];
				}
			}
			
			//å¦‚æœæ²¡æœ‰ä¸­é—´è§£æç»“æœï¼Œå°±é‡æ–°æ„å»º
			var startTexID:int = isWT ? wt.startID : 0;
			var startTexIDStroke:int = isWT ? wt.startIDStroke : 0;
			if (!sameTexData || sameTexData.length < 1) {
				var scaleky:String = null;
				if (scaleFontWithCtx) {
					CharPages.charRender.scale(Math.max(fontScaleX, 1.0), Math.max(fontScaleY, 1.0));
					if (fontScaleX > 1.0 || fontScaleY > 1.0)
						scaleky = "" + ((fontScaleX * 10) | 0) + ((fontScaleY * 10) | 0);
				}
				startTexID = -1;
				startTexIDStroke = -1;
				var stx:int = 0;
				var sty:int = 0;
				//ç›´æ¥è§£æå­—ç¬¦ä¸²
				_curStrPos = 0;
				var curstr:String;
				if (isHtmlChar) {
					var chc:HTMLChar = htmlchars[_curStrPos++];
					curstr = chc.char;
					stx = chc.x;
					sty = chc.y;
				} else {
					curstr = getNextChar(str);
				}
				var bold:Boolean = font._bold;
				while (curstr) {
					var ri:CharRenderInfo;
					ri = _curPage.getChar(curstr, lineWidth, font._size, color, strokeColor, bold, false, scaleky);
					if (!ri) {
						break;
					}
					ri.char = curstr;	//debug
					if (ri.isSpace) {
					} else {
						//åˆ†ç»„ä¿å­˜
						var add:Array = sameTexData[ri.tex.id];
						if (!add) {
							sameTexData[ri.tex.id] = add = [];
							if (startTexID < 0 || startTexID > ri.tex.id)
								startTexID = ri.tex.id;
						}
						//ä¸èƒ½ç›´æ¥ä¿®æ”¹ri.bmpWidth, å¦åˆ™ä¼šç´¯ç§¯ç¼©æ”¾ï¼Œæ‰€ä»¥æŠŠç¼©æ”¾ä¿å­˜åˆ°ç‹¬ç«‹çš„å˜é‡ä¸­
						//TODO ç°åœ¨strokeå’Œæ–‡å­—æ··åˆ°ä¸€èµ·äº†ï¼Œcolorå·²ç»æ²¡æ³•èµ·ä½œç”¨äº†ï¼Œæ‰€ä»¥ä¸‹é¢çš„colorå…¶å®æ²¡ç”¨äº†
						add.push({ri: ri, x: stx, y: sty, w: ri.bmpWidth / fontScaleX, h: ri.bmpHeight / fontScaleY});
					}
					if (isHtmlChar) {
						chc = htmlchars[_curStrPos++];
						if (chc) {
							curstr = chc.char;
							stx = chc.x;
							sty = chc.y;
						} else {
							curstr = null;
						}
					} else {
						curstr = getNextChar(str);
						stx += ri.width;
					}
					
				}
				if (isWT) {
					wt.startID = startTexID;
					wt.startIDStroke = startTexIDStroke;
				}
			}
			
			//var lastUseColor:Boolean = ctx._drawTextureUseColor;
			//PerfHUD.drawTexTm += (PerfHUD.inst.now() - st);
			//_drawResortedWords(ctx,curx,sameTexDataStroke,startTexIDStroke,y);
			_drawResortedWords(ctx, curx, sameTexData, startTexID, y);
			//ctx._drawTextureUseColor = lastUseColor;
			ctx._italicDeg = 0;
		}
		
		public function fillWords(ctx:WebGLContext2D, data:Vector.<HTMLChar>, x:Number, y:Number, fontStr:String, color:String, strokeColor:String, lineWidth:int):void {
			if (!data) return;
			if (data.length <= 0) return;
			var nColor:uint = ColorUtils.create(color).numColor;
			var nStrokeColor:uint = strokeColor ? ColorUtils.create(strokeColor).numColor : 0;
			_curFont = fontStr;
			var font:FontInfo = FontInfo.Parse(fontStr);
			_fast_filltext(ctx, null, data, x, y, font, color, strokeColor, lineWidth, 0, 0);
		}
		
		/**
		 *
		 * TEST
		 * 	emoji: 'ğŸ’—'
		 *  arabic: 'Ø³Ù„Ø§Ù…'
		 *  ç»„åˆ: 'aÌ†'
		 *  æ³°è¯­: 'à¸à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹à¹'
		 *  å¤©åŸæ–‡: 'à¤•à¤¿'		 *
		 */
		public function filltext(ctx:WebGLContext2D, data:String, x:Number, y:Number, fontStr:String, color:String, strokeColor:String, lineWidth:int, textAlign:String, underLine:int = 0):void {
			if (data.length <= 0)
				return;
			//ä»¥åä¿å­˜åˆ°wordtextä¸­
			var nColor:uint = ColorUtils.create(color).numColor;
			var nStrokeColor:uint = strokeColor ? ColorUtils.create(strokeColor).numColor : 0;
			_curFont = fontStr;
			var font:FontInfo = FontInfo.Parse(fontStr);
			
			var nTextAlign:int = 0;
			switch (textAlign) {
			case 'center': 
				nTextAlign = Context.ENUM_TEXTALIGN_CENTER;
				break;
			case 'right': 
				nTextAlign = Context.ENUM_TEXTALIGN_RIGHT;
				break;
			}
			_fast_filltext(ctx, data as WordText, null, x, y, font, color, strokeColor, lineWidth, nTextAlign, underLine);
		}
		
		//TODO:coverage
		public function filltext_native(ctx:WebGLContext2D, data:String, htmlchars:Vector.<HTMLChar>, x:Number, y:Number, fontStr:String, color:String, strokeColor:String, lineWidth:int, textAlign:String, underLine:int = 0):void {
			if (data && data.length <= 0)
				return;
			//ä»¥åä¿å­˜åˆ°wordtextä¸­
			var nColor:uint = ColorUtils.create(color).numColor;
			var nStrokeColor:uint = strokeColor ? ColorUtils.create(strokeColor).numColor : 0;
			_curFont = fontStr;
			//
			fontScaleX = fontScaleY = 1.0;
			if (scaleFontWithCtx) {
				var sx:Number = ctx._curMat.getScaleX();
				var sy:Number = ctx._curMat.getScaleY();
				if (sx < 1e-4 || sy < 1e-1)
					return;
				fontScaleX = sx;
				fontScaleY = sy;
				CharPages.charRender.scale(fontScaleX, fontScaleY);
			}
			var font:FontInfo = FontInfo.Parse(fontStr);
			var fontFamily:String = font._family;
			var bold:Boolean = font._bold;
			
			if (font._italic) {
				ctx._italicDeg = 12;
			}
			
			_curPage = selectFont(fontFamily, font._size * fontScaleX);
			//å‡†å¤‡bmp
			//æ‹·è´åˆ°textureä¸Š,å¾—åˆ°ä¸€ä¸ªgltextureå’Œuv
			var curx:Number = x;
			var wt:WordText = data as WordText;
			var str:String = data;
			var strWidth:Number = 0;
			var isWT:Boolean = !htmlchars && (str is WordText);
			var isHtmlChar:Boolean = !!htmlchars;
			
			/**
			 * sameTexData
			 * ä¸ºäº†åˆå¹¶ç›¸åŒè´´å›¾çš„ï¼Œè¦æ ¹æ®è´´å›¾æ¥åˆ†ç»„
			 * ä¸‹æ ‡æ˜¯textureid,å†…å®¹æ˜¯ä¸€ä¸ªvectorï¼Œä¿å­˜çš„æ˜¯{color:uint,ri:CharRenderInfo,x}
			 */
			var sameTexData:Array = (cacheRenderInfoInWordText && isWT) ? wt.pageChars : [];
			
			//æ€»å®½åº¦ï¼Œä¸‹é¢çš„å¯¹é½éœ€è¦
			if (isWT) {
				str = wt.toString();
				strWidth = wt.width;
				if (strWidth < 0) {
					strWidth = wt.width = _curPage.getWidth(str);
				}
			} else {
				strWidth = _curPage.getWidth(str);
			}
			
			//æ°´å¹³å¯¹é½æ–¹å¼
			switch (textAlign) {
			case 'center': 
				curx = x - strWidth / 2;
				break;
			case 'right': 
				curx = x - strWidth;
				break;
			default: 
				curx = x;
			}
			
			//æ£€æŸ¥ä¿å­˜çš„æ•°æ®æ˜¯å¦æœ‰çš„å·²ç»è¢«é‡Šæ”¾äº†
			if (sameTexData) {
				var needRebuild:Boolean = false;
				for (var i:int = 0, sz:int = sameTexData.length; i < sz; i++) {
					var pri:* = sameTexData[i];
					if (!pri) continue;
					for (var j:int = 0, pisz:int = pri.length; j < pisz; j++) {
						var riSaved:* = pri[j];
						if (riSaved.ri.tex.__destroyed) {
							needRebuild = true;
							break;
						}
					}
					if (needRebuild) break;
				}
				if (needRebuild)
					sameTexData = wt.pageChars = [];
			}
			
			//å¦‚æœæ²¡æœ‰æŠŠä¸­é—´è§£æç»“æœï¼Œå°±é‡æ–°æ„å»º
			if (!sameTexData || sameTexData.length <= 0) {
				var scaleky:String = null;
				if (scaleFontWithCtx) {
					CharPages.charRender.scale(Math.max(fontScaleX, 1.0), Math.max(fontScaleY, 1.0));
					if (fontScaleX > 1.0 || fontScaleY > 1.0)
						scaleky = "" + ((fontScaleX * 10) | 0) + ((fontScaleY * 10) | 0);
				}
				
				var stx:int = 0;
				var sty:int = 0;
				//ç›´æ¥è§£æå­—ç¬¦ä¸²
				_curStrPos = 0;
				var curstr:String;
				if (isHtmlChar) {
					var chc:HTMLChar = htmlchars[_curStrPos++];
					curstr = chc.char;
					stx = chc.x;
					sty = chc.y;
				} else {
					curstr = getNextChar(str);
				}
				bold = font._bold;
				while (curstr) {
					var ri:CharRenderInfo;
					ri = _curPage.getChar(curstr, lineWidth, font._size, color, strokeColor, bold, false, scaleky);
					if (ri.isSpace) {
					} else {
						//åˆ†ç»„ä¿å­˜
						var add:Array = sameTexData[ri.tex.id];
						if (!add) {
							sameTexData[ri.tex.id] = add = [];
						}
						add.push({ri: ri, x: stx, y: sty, color: color, nColor: nColor});
						
					}
					
					if (isHtmlChar) {
						chc = htmlchars[_curStrPos++];
						if (chc) {
							curstr = chc.char;
							stx = chc.x;
							sty = chc.y;
						} else {
							curstr = null;
						}
					} else {
						stx += ri.width;
						curstr = getNextChar(str);
					}
				}
			}
			
			var lastUseColor:Boolean = ctx._drawTextureUseColor;
			_drawResortedWords_native(ctx, curx, sameTexData, y);
			ctx._drawTextureUseColor = lastUseColor;
			ctx._italicDeg = 0;
		}
		
		/**
		 * ç”»å‡ºé‡æ–°æŒ‰ç…§è´´å›¾é¡ºåºåˆ†ç»„çš„æ–‡å­—ã€‚
		 * @param	samePagesData
		 * @param  startx ä¿å­˜çš„æ•°æ®æ˜¯ç›¸å¯¹ä½ç½®ï¼Œæ‰€ä»¥éœ€è¦åŠ ä¸Šè¿™ä¸ªä¾¿å®œï¼Œç›¸å¯¹ä½ç½®æ›´çµæ´»ä¸€äº›ã€‚
		 * @param y {int} å› ä¸ºè¿™ä¸ªåªèƒ½ç”»åœ¨ä¸€è¡Œä¸Šæ‰€ä»¥æ²¡æœ‰å¿…è¦ä¿å­˜yã€‚æ‰€ä»¥è¿™é‡Œå†æŠŠyä¼ è¿›æ¥
		 */
		protected function _drawResortedWords(ctx:WebGLContext2D, startx:int, samePagesData:Array, startID:int, y:int):void {
			var lastColor:uint = ctx.getFillColor();
			ctx.setFillColor(ctx.mixRGBandAlpha(0xffffff));
			
			startx -= _curPage.margin_left; 	//å› ä¸ºè¿”å›çš„uvæ˜¯åŠ ä¸Šmarginä¹‹åçš„ï¼Œæ‰€ä»¥éœ€è¦å†å‡å»marginæ‰æ˜¯å¸Œæœ›çš„ä½ç½®
			y -= _curPage.margin_top;
			var isLastRender:Boolean = ctx._charSubmitCache._enbale;
			for (var i:int = startID, sz:int = samePagesData.length; i < sz; i++) {
				var pri:Array = samePagesData[i];
				if (!pri) continue;
				var pisz:int = pri.length;
				if (pisz <= 0) continue;
				//ctx._useNewTex2DSubmit(tex, pisz * 4);
				for (var j:int = 0; j < pisz; j++) {
					var riSaved:* = pri[j];
					var ri:CharRenderInfo = riSaved.ri;
					if (ri.isSpace) continue;
					ri.touch();
					//ctx._drawTexRect(startx + riSaved.x, y + riSaved.y, riSaved.w, riSaved.h, ri.uv);
					//_drawCharRenderInfo(ctx, riSaved.ri, startx + riSaved.x, y + riSaved.y);
					ctx.drawTexAlign = true;
					ctx._inner_drawTexture(ri.tex.texture as Texture, (ri.tex.texture as Texture).bitmap.id,startx +riSaved.x , y + riSaved.y , riSaved.w, riSaved.h, null,ri.uv,1.0,isLastRender);
					if ((ctx as Object).touches) {
						(ctx as Object).touches.push(ri);
					}
				}
			}
			ctx.setFillColor(lastColor);
			//ä¸è¦å½±å“åˆ«äºº
			//ctx._curSubmit._key.other = -1;
		}
		
		//TODO:coverage
		protected function _drawResortedWords_native(ctx:WebGLContext2D, startx:int, samePagesData:Array, y:int):void {
			var lastcolor:uint = 0;
			for (var i:int = 0, sz:int = samePagesData.length; i < sz; i++) {
				var pri:* = samePagesData[i];
				if (!pri) continue;
				for (var j:int = 0, pisz:int = pri.length; j < pisz; j++) {
					var riSaved:* = pri[j];
					var ri:CharRenderInfo = riSaved.ri;
					if (ri.isSpace) continue;
					ctx._drawTextureUseColor = false;
					if (lastcolor != riSaved.nColor) {
						ctx.fillStyle = riSaved.color;
						lastcolor = riSaved.nColor;
					}
					ri.touch();
					_drawCharRenderInfo(ctx, riSaved.ri, startx + riSaved.x, riSaved.y + y);
				}
			}
		}
		
		/**
		 * ç”»å‡ºä¿å­˜åœ¨riä¸­çš„æ–‡å­—ä¿¡æ¯ã€‚
		 * @param	ctx
		 * @param	ri
		 * @param	x
		 * @param	y
		 */
		//TODO:coverage
		protected function _drawCharRenderInfo(ctx:WebGLContext2D, ri:CharRenderInfo, x:int, y:int):void {
			//å› ä¸ºè¿”å›çš„uvæ˜¯åŠ ä¸Šmarginä¹‹åçš„ï¼Œæ‰€ä»¥éœ€è¦å†å‡å»marginæ‰æ˜¯å¸Œæœ›çš„ä½ç½®ã€‚TODO left,topåº”è¯¥æ˜¯0æŠŠ
			ctx._drawTextureM(ri.tex.texture as Texture, x - _curPage.margin_left, y - _curPage.margin_top, ri.bmpWidth, ri.bmpHeight, null, 1.0, ri.uv);
			if ((ctx as Object).touches) {
				(ctx as Object).touches.push(ri);
			}
		}
		
		// for debug
		public function listPages():void {
			console.log('æ‰“å°æ‰€æœ‰é¡µçš„ä¿¡æ¯:');
			fontPages.forEach(function(cp:CharPages, i:int):void { 
				var name:String = fontPagesName[i];
				var minsz:int = parseInt(name.substr(cp.fontFamily.length)) * gridSize;
				console.log('===================================');
				console.log('åå­—:', fontPagesName[i],'å¤§å°èŒƒå›´:', minsz, minsz+gridSize);
				cp.printPagesInfo();
			} );
		}
		
		/**
		 * åƒåœ¾å›æ”¶
		 */
		public function GC(force:Boolean):void {
			var i:int = 0, sz:int = fontPages.length;
			fontPages.forEach(function(p:CharPages):void { p.removeLRU(); } );
			/*
			if (sz) {
				var curCleanID:int = Stat.loopCount % sz;
				(fontPages[curCleanID] as CharPages).removeLRU();
			}
			*/
		}
	}
}

import laya.webgl.resource.CharPageTexture;

/**
 * charPageTextureçš„ç¼“å­˜ç®¡ç†ï¼Œåæ­£æ‰€æœ‰çš„è´´å›¾å¤§å°éƒ½æ˜¯ä¸€è‡´çš„ï¼Œå®Œå…¨å¯ä»¥é‡å¤åˆ©ç”¨ã€‚
 */
class charPageTrash {
	private var pool:Array = new Array(10);
	private var poolLen:int = 0;
	private var cleanTm:Number = 0;
	public var texW:int = 0;	//CharPageTextureçš„å®½åº¦
	
	/**
	 *
	 * @param	w æ¯å¼ è´´å›¾çš„å®½é«˜
	 */
	public function charPageTrash(w:int) {
		texW = w;
	}
	
	public function getAPage(gridnum:int):CharPageTexture {
		if (poolLen > 0) {
			var ret:CharPageTexture = pool[--poolLen];
			ret.setGridNum(gridnum);
			if (poolLen > 0)
				clean();	//ç»™ä¸ªcleançš„æœºä¼šã€‚
			return ret;
		}
		//è¿™ç§æƒ…å†µä¸éœ€è¦clean
		return new CharPageTexture(texW, texW, gridnum);
	}
	
	public function discardPage(p:CharPageTexture):void {
		clean();
		if (!p) return;
		p.genID++;		// ç”¨æ¥åˆ¤æ–­é‡Šæ”¾
		if (poolLen >= pool.length) {
			pool = pool.concat(new Array(10));
		}
		p.reset();
		pool[poolLen++] = p;
	}
	
	/**
	 * å®šæœŸæ¸…ç†
	 * ä¸ºäº†ç®€å•ï¼Œåªæœ‰å‘ç”Ÿ getAPage æˆ–è€… discardPageçš„æ—¶å€™æ‰æ£€æµ‹æ˜¯å¦éœ€è¦æ¸…ç†
	 */
	private function clean():void {
		var curtm:Number = Laya.stage.getFrameTm();
		if (cleanTm === 0) cleanTm = curtm;
		if (curtm - cleanTm > 30000) {	//æ¯30ç§’æ¸…ç†ä¸€ä¸‹
			for (var i:int = 0; i < poolLen; i++) {
				var p:CharPageTexture = pool[i];
				if (curtm - p._discardTm > 20000) {//è¶…è¿‡20ç§’æ²¡ç”¨çš„åˆ æ‰
					p.destroy();					//çœŸæ­£åˆ é™¤è´´å›¾
					pool[i] = pool[poolLen - 1];
					poolLen--;
					i--;	//è¿™ä¸ªè¿˜è¦å¤„ç†ï¼Œç”¨æ¥æŠµæ¶ˆi++
				}
			}
			cleanTm = curtm;
		}
	}
}
